RobotSystem testCase2016Project

Event local_inputDrive : local_inputDrive(X)	//events from GUI/External Input
Dispatch drive : drive(X)
Dispatch detectionResults : detectionResults(X)	
Event alarm : alarm
Event local_alarm : local_alarm					//events from GUI/External Input
Event obstacle : obstacle(X)
Event bagFound : bagFound
Event endDetection : endDetection

Context ctxDriveRobot ip[host="192.168.1.69" port=8010]
Context ctxOperator ip[host="192.168.1.2" port=8015]
Context ctxASC ip[host="192.168.1.2" port=8020]

QActor led context ctxDriveRobot 
{	
	Plan init normal
		println("Led starts");
		solve consult("ledTheory.pl") time(0); 
		switchToPlan senseStartBlink
		
	Plan senseStartBlink
		println("Led Off");
		solve turnTheLed(off) time(0);
		sense time(60000) bagFound -> startBlinking;
		repeatPlan 0
		
	Plan startBlinking
		println("led On");
		solve turnTheLed(on) time(0);
		delay time(1000) react event endDetection -> senseAlarm;
		println("Led Off");
		solve turnTheLed(off) time(0);
		delay time(1000) react event endDetection -> senseAlarm;
		repeatPlan 0
		
	Plan senseAlarm
		println("Led Off");
		solve turnTheLed(off) time(0);
		sense time(60000) alarm-> startBlinking;
		repeatPlan 0
}

QActor operator context ctxOperator -g cyan
{
	Plan init normal
		println("Operator starts");
		switchToPlan senseInput
		
	Plan senseInput
		sense time(60000) local_inputDrive -> sendDriveCommands;
		repeatPlan 0
		
	Plan sendDriveCommands resumeLastPlan
		onEvent local_inputDrive : local_inputDrive(X) -> forward driverobot -m drive : drive(X)	
}

QActor asc context ctxASC -g green
{
	Plan init normal
		println("ASC starts");
		switchToPlan work
		
	Plan work
		receiveMsg time(600000);
		onMsg detectionResults : detectionResults(X) -> 
		solve actorOp(loadResults(X)) time(0) onFailSwitchTo prologFailure;
		switchToPlan senseAlarm
		
	Plan senseAlarm
		sense time(100000) local_alarm -> continue;
		onEvent local_alarm : local_alarm -> emit alarm : alarm
		
	Plan prologFailure resumeLastPlan
		println("Prolog failure")
}

Robot plexiBox QActor driverobot context ctxDriveRobot 
{
	Plan init normal
		println("driverobot starts");
		solve consult("talkTheory.pl") time(0) onFailSwitchTo prologFailure;
		solve consult("driveRobotTheory.pl") time(0) onFailSwitchTo prologFailure;
		switchToPlan receiveFirstCommand
		
	Plan receiveFirstCommand
		receiveMsg time(600000) react event obstacle -> detect;
		//Save first sender
		[?? msg(drive,dispatch, S, R, drive(X), N)] solve assert(firstSender(S)) time(0);
		onMsg drive : drive(X) -> solve savemove(X) time(0) onFailSwitchTo savemoveFailure;
		onMsg drive : drive(X) -> solve X time(0) onFailSwitchTo prologFailure;
		onMsg drive : drive(X) -> switchToPlan drive;
		repeatPlan 0
	
	Plan drive 
		receiveMsg time(600000) react event obstacle -> detect;
		//To make sure that the sender is the same as the first one
		[?? msg(drive,dispatch, S, R, drive(X), N)] solve firstSender(S) time(0) onFailSwitchTo drive;
		onMsg drive : drive(X) -> solve savemove(X) time(0) onFailSwitchTo prologFailure;
		onMsg drive : drive(X) -> solve X time(0) onFailSwitchTo prologFailure;
		//[!? moveList(X)] println(moveList(X));	//DEBUG
		//[!? lastMove(X,Y)] println(lastMove(X,Y));	//DEBUG
		repeatPlan 0
		
	Plan detect
		println("Stopping...");
		solve endSavemoves time(0);
		robotStop speed(100) time(1000);
		emit bagFound : bagFound;
		println("Starting detection Phase...");
		[!? detection(X) ] forward asc -m detectionResults : detectionResults(X);
		println("Detection Results Sent");
		emit endDetection : endDetection;
		println("Back to base");
		switchToPlan backToBase
		
	Plan backToBase
		solve backToBase time(0);	//It doesn't need to react, as the qactor led handles that
		switchToPlan finish
		
	Plan finish
		println("DriveRobot ends")
		
	Plan prologFailure resumeLastPlan
		println("Failed to load prolog theories")
		
	Plan savemoveFailure resumeLastPlan
		println("Failed save move")
	
}
